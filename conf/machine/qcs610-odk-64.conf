#@TYPE: Machine
#@NAME: qcs610-odk (64-bit)
#@DESCRIPTION: Machine configuration for qcs610-odk in 64 bits mode

require conf/machine/include/qcs610.inc

MACHINE_SUPPORTS_DTBO  = "True"

KERNEL_DTB_NAMES = "${@bb.utils.contains('MACHINE_SUPPORTS_DTBO', 'True', "${DTBO_DTB_NAMES}", "${NON_DTBO_DTB_NAMES}", d)}"

KERNEL_IMAGETYPE   = "Image.gz"
DTBO_DTB_NAMES     = "vendor/qcom/qcs610.dtb vendor/qcom/qcs410.dtb"
NON_DTBO_DTB_NAMES = "vendor/qcom/qcs610-iot.dtb vendor/qcom/qcs410-opk.dtb"
KERNEL_BASE        = "0x80000000"
KERNEL_TAGS_OFFSET = "0x81e00000"

KERNEL_CONFIG ?= "vendor/sm6150-qgki_defconfig"
TARGET_BUILD_VARIANT ?= "user"
KERNEL_CONFIG_qti-distro-debug ?= "vendor/sm6150-qgki-debug_defconfig"
TARGET_BUILD_VARIANT_qti-distro-debug ?= "debug"

CONSOLE_PARAM ?= ""
CONSOLE_PARAM_qti-distro-debug ?= "console=ttyMSM0,115200,n8"

KERNEL_CMD_PARAMS ?= "noinitrd rootwait ${CONSOLE_PARAM} androidboot.hardware=qcom androidboot.console=ttyMSM0 lpm_levels.sleep_disabled=1 service_locator.enable=1 firmware_class.path=/lib/firmware/updates msm_rtb.filter=0x237"

SERIAL_CONSOLE    ?= ""
SERIAL_CONSOLE_qti-distro-debug ?= "115200 ttyMSM0"

# bootloader configuration
EXTRA_IMAGEDEPENDS += "edk2"

# Conf with partition entries required for machine.
MACHINE_PARTITION_CONF = "qcs610-odk-partition.conf"

# filesystem configurations to be applied on image.
MACHINE_FSCONFIG_CONF = "qcs610-fsconfig.conf"

# File indicating firmware residing places on device.
MACHINE_FILESMAP_CONF = "qcs610-filesmap"

# Build architecture configuration
DEFAULTTUNE = "aarch64"

KERNEL_DYNAMIC_DEFCONFIG = "True"


## Machine features ##
# emmc-boot:             Machine supports eMMC device
# drm:                   Machine supports DRM display drivers
# ubwc:                  Machine supports UBWC hardware
# qti-emac-dwc-eqos:     Machine supports emac-dwc-eqos driver
# qti-afr-algo:  Support QTI Auto framing algo
# qti-audio:             Machine supports QTI audio solution
# qti-bluetooth:         Machine supports QTI bluetooth solution
# qti-camera:            Machine supports QTI camera solution
# qti-cdsp:              Machine supports QTI cdsp solution
# qti-adsp:              Machine supports QTI adsp solution
# qti-ab-boot:           Machine supports a/b boot solution
# qti-display:           Machine supports QTI display solution
# qti-helium:            Machine supports QTI helium WiFi target
# qti-sensors:           Machine supports sensors qti solution
# tflite-hw-delegate:    Machine supports TFlite hardware delegates
# qti-tflite-delegate:   Machine supports Tensorflow lite hardware delegates
# qti-video:             Machine supports QTI video solution
# qti-wifi:              Machine supports wifi solution
# opencl:                Machine supports OpenCL driver
# dm-verity-bootloader:  Machine supports bootloader-based dm-verity implementation
# qti-location:          Machine supports Location solution

MACHINE_FEATURES_append = " emmc-boot drm ubwc qti-audio qti-afr-algo qti-camera qti-cdsp qti-adsp qti-ab-boot qti-display qti-helium qti-sensors tflite-hw-delegate qti-tflite-delegate opencl qti-security qti-video dm-verity-bootloader qti-location qti-wifi"
